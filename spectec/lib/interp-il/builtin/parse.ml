open Il.Ast
open Xl
open Util.Source

type 'a t = region -> Value.t -> ('a, Err.t) result

let value : Value.t t = fun _at v -> Ok v
let ( let* ) = Result.bind

let text : string t =
 fun at v ->
  match v.it with
  | TextV s -> Ok s
  | _ -> Error (Err.type_err at "Expected TextV" v)

let bool : bool t =
 fun at v ->
  match v.it with
  | BoolV b -> Ok b
  | _ -> Error (Err.type_err at "Expected BoolV" v)

let nat : Bigint.t t =
 fun at v ->
  match v.it with
  | NumV (`Nat n) -> Ok n
  | _ -> Error (Err.type_err at "Expected Nat NumV" v)

let int : Bigint.t t =
 fun at v ->
  match v.it with
  | NumV (`Int n) -> Ok n
  | _ -> Error (Err.type_err at "Expected Int NumV" v)

let result_all (l : ('a, Err.t) result list) : ('a list, Err.t) result =
  let rec aux acc = function
    | [] -> Ok (List.rev acc)
    | x :: xs -> (
        match x with Ok y -> aux (y :: acc) xs | Error e -> Error e)
  in
  aux [] l

let pair : (Value.t * Value.t) t =
 fun at v ->
  match v.it with
  | TupleV [ a; b ] -> Ok (a, b)
  | _ -> Error (Err.type_err at "Expected a 2-tuple" v)

let list_of (p : 'a t) : 'a list t =
 fun at v ->
  match v.it with
  | ListV vs -> result_all (List.map (p at) vs)
  | _ -> Error (Err.type_err at "Expected ListV" v)

module VSet = Set.Make (Value)

let set : VSet.t t =
 fun at v ->
  match v.it with
  | CaseV (_, [ elements ]) -> (
      match elements.it with
      | ListV vs -> Ok (VSet.of_list vs)
      | _ -> Error (Err.type_err at "Expected set's inner value to be a list" v)
      )
  | _ -> Error (Err.type_err at "Expected a set value" v)

module VMap = Map.Make (Value)

(** Parses a single 'k:v' pair (CaseV) *)
let colon_pair : (Value.t * Value.t) t =
 fun at v ->
  match v.it with
  | CaseV ([ []; [ { it = Atom.Colon; _ } ]; [] ], [ k; v ]) -> Ok (k, v)
  | _ -> Error (Err.type_err at "Expected a 'k:v' pair" v)

(** Parses a map value { ... } into an OCaml VMap.t *)
let map : Value.t VMap.t t =
 fun at v ->
  match v.it with
  | CaseV
      ( [ [ { it = Atom.LBrace; _ } ]; [ { it = Atom.RBrace; _ } ] ],
        [ pair_list_val ] ) ->
      let* pairs = (list_of colon_pair) at pair_list_val in
      Ok (VMap.of_list pairs)
  | _ -> Error (Err.type_err at "Expected a map value" v)

let all_of (p : 'a t) (at : region) (vs : value list) : ('a list, Err.t) result
    =
  result_all (List.map (p at) vs)
