open Util.Error
open Util.Source

let version = "0.1"

(* Statistics *)

type stat = { durations : float list; exclude_run : int; fail_run : int }

let empty_stat = { durations = []; exclude_run = 0; fail_run = 0 }

let log_stat name stat total : unit =
  let excludes = stat.exclude_run in
  let fails = stat.fail_run in
  let passes = total - excludes - fails in
  let exclude_rate = float_of_int excludes /. float_of_int total *. 100.0 in
  let pass_rate = float_of_int passes /. float_of_int total *. 100.0 in
  let fail_rate = float_of_int fails /. float_of_int total *. 100.0 in
  let durations = List.sort compare stat.durations in
  let duration_total = List.fold_left ( +. ) 0.0 durations in
  let duration_avg = duration_total /. float_of_int total in
  let duration_max = durations |> List.rev |> List.hd in
  let duration_min = durations |> List.hd in
  Format.asprintf
    "%s: [EXCLUDE] %d/%d (%.2f%%) [PASS] %d/%d (%.2f%%) [FAIL] %d/%d (%.2f%%)"
    name excludes total exclude_rate passes total pass_rate fails total
    fail_rate
  |> print_endline;
  Format.eprintf "%s: [TOTAL] %.6f [AVG] %.6f [MAX] %.6f [MIN] %.6f\n" name
    duration_total duration_avg duration_max duration_min

(* Exceptions *)

exception TestCheckErr of string * region * float
exception TestCheckNegErr of float
exception TestParseFileErr of string * region * float
exception TestParseStringErr of string * region * float
exception TestParseRoundtripErr of float
exception TestUnknownErr of float

(* Timer *)

let start () = Unix.gettimeofday ()
let stop start = Unix.gettimeofday () -. start

(* File collector *)

(* (* helper: string suffix *) *)
(* let ends_with ~suffix (s : string) : bool = *)
(*   let ls = String.length s and lsf = String.length suffix in *)
(*   lsf <= ls && String.sub s (ls - lsf) lsf = suffix *)

let rec collect_files ~(suffix : string) dir =
  let files = Sys_unix.readdir dir in
  Array.sort String.compare files;
  Array.fold_left
    (fun files file ->
      let filename = dir ^ "/" ^ file in
      if Sys_unix.is_directory_exn filename && file <> "include" then
        files @ collect_files ~suffix filename
      else if String.ends_with ~suffix filename then files @ [ filename ]
      else files)
    [] files

(* Exclude collector *)

let collect_exclude filename_exclude =
  let ic = open_in filename_exclude in
  let rec parse_lines excludes =
    try
      let exclude = "../../../../../0_p4-spectec/" ^ input_line ic in
      let excludes = exclude :: excludes in
      parse_lines excludes
    with End_of_file -> excludes
  in
  let excludes = parse_lines [] in
  close_in ic;
  excludes

let collect_excludes (paths_exclude : string list) =
  let filenames_exclude =
    List.concat_map (collect_files ~suffix:".exclude") paths_exclude
  in
  List.concat_map collect_exclude filenames_exclude

(* Spec Elaboration test *)

let elab specdir =
  let parsed =
    specdir
    |> collect_files ~suffix:".spectec"
    |> List.concat_map Frontend.Parse.parse_file
  in
  match Elaborate.Elab.elab_spec parsed with
  | Elaborate.Elab.Spec spec_il -> spec_il
  | Elaborate.Elab.Errors errors -> (
      (* propagate the first elaboration error so callers can handle it *)
      match errors with
      | (at, failtraces) :: _ -> raise (ElabError (at, failtraces))
      | [] -> failwith "elab_spec returned Errors []")

let elab_test specdir =
  let spec_il = elab specdir in
  Il.Ast.Print.string_of_spec spec_il |> print_endline

let elab_command =
  Core.Command.basic ~summary:"run elaboration test"
    (let open Core.Command.Let_syntax in
     let open Core.Command.Param in
     let%map specdir = flag "-s" (required string) ~doc:"p4 spec directory" in
     fun () ->
       try elab_test specdir with
       | ParseError (at, msg) ->
           Format.printf "Error on elaboration: %s\n" (string_of_error at msg)
       | ElabError (at, failtraces) ->
           Format.printf "Error on elaboration: %s\n"
             (string_of_elab_errors [ (at, failtraces) ]))

(* P4 Parser test *)

let parse_file time_start includes filename =
  try P4.Parse.parse_file includes filename
  with ParseError (at, msg) -> raise (TestParseFileErr (msg, at, time_start))

let parse_string time_start filename program_dump =
  try P4.Parse.parse_string filename program_dump
  with ParseError (at, msg) ->
    raise (TestParseStringErr (msg, at, time_start))

let parse_roundtrip time_start includes filename spec =
  let program = parse_file time_start includes filename in
  let program_dump =
    Format.asprintf "%a\n" (Concrete.Pp.pp_program spec) program
  in
  let program_roundtrip = parse_string time_start filename program_dump in
  if not (Il.Ast.Eq.eq_value ~dbg:true program program_roundtrip) then
    raise (TestParseRoundtripErr time_start)
  else time_start

let run_parser includes filename spec =
  let time_start = start () in
  try parse_roundtrip time_start includes filename spec with
  | TestParseFileErr _ as err -> raise err
  | TestParseStringErr _ as err -> raise err
  | TestParseRoundtripErr _ as err -> raise err
  | _ -> raise (TestUnknownErr time_start)

let run_parser_test stat includes excludes filename spec =
  if List.exists (String.equal filename) excludes then (
    let log = Format.asprintf "Excluding file: %s" filename in
    log |> print_endline;
    {
      stat with
      durations = 0.0 :: stat.durations;
      exclude_run = stat.exclude_run + 1;
    })
  else
    try
      let time_start = run_parser includes filename spec in
      let duration = stop time_start in
      let log = Format.asprintf "Parser roundtrip success: %s" filename in
      log |> print_endline;
      Format.eprintf "%s\n" log;
      Format.eprintf ">>> took %.6f seconds\n" duration;
      { stat with durations = duration :: stat.durations }
    with
    | TestParseFileErr (msg, at, time_start) ->
        let duration = stop time_start in
        let log =
          Format.asprintf "Error parsing file: %s\n%s" filename
            (string_of_error at msg)
        in
        log |> print_endline;
        Format.eprintf "%s\n" log;
        Format.eprintf ">>> took %.6f seconds\n" duration;
        {
          stat with
          durations = duration :: stat.durations;
          fail_run = stat.fail_run + 1;
        }
    | TestParseStringErr (msg, at, time_start) ->
        let duration = stop time_start in
        let log =
          Format.asprintf "Error parsing string: %s\n%s" filename
            (string_of_error at msg)
        in
        log |> print_endline;
        Format.eprintf "%s\n" log;
        Format.eprintf ">>> took %.6f seconds\n" duration;
        {
          stat with
          durations = duration :: stat.durations;
          fail_run = stat.fail_run + 1;
        }
    | TestParseRoundtripErr time_start ->
        let duration = stop time_start in
        let log = Format.asprintf "Error roundtripping parser: %s" filename in
        log |> print_endline;
        Format.eprintf "%s\n" log;
        Format.eprintf ">>> took %.6f seconds\n" duration;
        {
          stat with
          durations = duration :: stat.durations;
          fail_run = stat.fail_run + 1;
        }
    | TestUnknownErr time_start ->
        let duration = stop time_start in
        let log = Format.asprintf "Unknown error on parser: %s" filename in
        log |> print_endline;
        Format.eprintf "%s\n" log;
        Format.eprintf ">>> took %.6f seconds\n" duration;
        {
          stat with
          durations = duration :: stat.durations;
          fail_run = stat.fail_run + 1;
        }

let run_parser_test_driver includes excludes testdir specdir =
  (* Force all debug levels to Quiet for testing *)
  Unix.putenv "P4SPEC_LEXER_DEBUG" "quiet";
  Unix.putenv "P4SPEC_PARSER_DEBUG" "quiet";
  Unix.putenv "P4SPEC_CONTEXT_DEBUG" "quiet";
  let excludes = collect_excludes excludes in
  let filenames = collect_files ~suffix:".p4" testdir in
  let spec = elab specdir in
  let total = List.length filenames in
  let stat = empty_stat in
  Format.asprintf "Running parser tests on %d files\n" total |> print_endline;
  let stat =
    List.fold_left
      (fun stat filename ->
        Format.asprintf "\n>>> Running parser test on %s" filename
        |> print_endline;
        run_parser_test stat includes excludes filename spec)
      stat filenames
  in
  log_stat "\nRunning parser" stat total

let run_parser_command =
  Core.Command.basic ~summary:"run parser test on P4 files"
    (let open Core.Command.Let_syntax in
     let open Core.Command.Param in
     let%map includes = flag "-i" (listed string) ~doc:"p4 include paths"
     and excludes = flag "-e" (listed string) ~doc:"p4 test exclude paths"
     and testdir = flag "-d" (required string) ~doc:"p4 test directory"
     and specdir = flag "-s" (required string) ~doc:"p4 spec directory" in
     fun () -> run_parser_test_driver includes excludes testdir specdir)

(* IL interpreter test *)

let run_il negative spec_il includes_p4 filename_p4 =
  let time_start = start () in
  try
    let vid_counter = ref 0 in
    let tid_counter = ref 0 in
    Effect.Deep.try_with
      (fun () ->
        (* Parse target P4 program *)
        let value_program = P4.Parse.parse_file includes_p4 filename_p4 in
        (* Initialize runner and run typing relation *)
        let ctx_init = Interp_il.Runner.init filename_p4 in
        let _ctx, _values =
          Interp_il.Runner.run_relation ctx_init spec_il "Program_ok"
            [ value_program ]
        in
        if negative then raise (TestCheckNegErr time_start);
        time_start)
      ()
      {
        effc =
          (fun (type a) (eff : a Effect.t) ->
            match eff with
            | Il.Ast.FreshVid ->
                Some
                  (fun (k : (a, _) Effect.Deep.continuation) ->
                    let id = !vid_counter in
                    incr vid_counter;
                    Effect.Deep.continue k (fun () -> id))
            | Il.Ast.ValueCreated _ ->
                Some
                  (fun (k : (a, _) Effect.Deep.continuation) ->
                    (* No-op *)
                    Effect.Deep.continue k ())
            | Il.Ast.FreshTid ->
                Some
                  (fun (k : (a, _) Effect.Deep.continuation) ->
                    let tid = "FRESH__" ^ string_of_int !tid_counter in
                    incr tid_counter;
                    Effect.Deep.continue k (fun () -> tid))
            | _ -> None (* Other effects *));
      }
  with
  | Util.Error.InterpError (at, msg) ->
      raise (TestCheckErr (msg, at, time_start))
  | Util.Error.ParseError (at, msg) ->
      raise (TestCheckErr (msg, no_region, time_start))
  | TestCheckErr _ as err -> raise err
  | TestCheckNegErr _ as err -> raise err
  | _ -> raise (TestUnknownErr time_start)

let run_il_test negative stat spec_il includes_p4 excludes_p4 filename_p4 =
  if List.exists (String.equal filename_p4) excludes_p4 then (
    let log = Format.asprintf "Excluding file: %s" filename_p4 in
    log |> print_endline;
    {
      stat with
      durations = 0.0 :: stat.durations;
      exclude_run = stat.exclude_run + 1;
    })
  else
    try
      let time_start = run_il negative spec_il includes_p4 filename_p4 in
      let duration = stop time_start in
      let log = Format.asprintf "Typecheck success: %s" filename_p4 in
      log |> print_endline;
      Format.eprintf "%s\n" log;
      Format.eprintf ">>> took %.6f seconds\n" duration;
      { stat with durations = duration :: stat.durations }
    with
    | TestCheckErr (msg, at, time_start) ->
        let duration = stop time_start in
        let log =
          Format.asprintf "Error on typecheck: %s\n%s" filename_p4
            (string_of_error at msg)
        in
        log |> print_endline;
        Format.eprintf "%s\n" log;
        Format.eprintf ">>> took %.6f seconds\n" duration;
        {
          stat with
          durations = duration :: stat.durations;
          fail_run = stat.fail_run + 1;
        }
    | TestCheckNegErr time_start ->
        let duration = stop time_start in
        let log = Format.asprintf "Error on typecheck: should fail" in
        log |> print_endline;
        Format.eprintf "%s\n" log;
        Format.eprintf ">>> took %.6f seconds\n" duration;
        { stat with durations = duration :: stat.durations }
    | TestUnknownErr time_start ->
        let duration = stop time_start in
        let log = Format.asprintf "Error on typecheck: unknown" in
        log |> print_endline;
        Format.eprintf "%s\n" log;
        Format.eprintf ">>> took %.6f seconds\n" duration;
        {
          stat with
          durations = duration :: stat.durations;
          fail_run = stat.fail_run + 1;
        }

let run_il_test_driver negative specdir includes_p4 excludes_p4 testdir_p4 =
  let spec_il = elab specdir in
  let excludes_p4 = collect_excludes excludes_p4 in
  let filenames_p4 = collect_files ~suffix:".p4" testdir_p4 in
  let total = List.length filenames_p4 in
  let stat = empty_stat in
  Format.asprintf "Running typing test on %d files\n" total |> print_endline;
  let stat =
    List.fold_left
      (fun stat filename_p4 ->
        Format.asprintf "\n>>> Running typing test on %s" filename_p4
        |> print_endline;
        run_il_test negative stat spec_il includes_p4 excludes_p4 filename_p4)
      stat filenames_p4
  in
  log_stat "\nRunning typing" stat total

let run_il_command =
  Core.Command.basic ~summary:"run typing test on IL"
    (let open Core.Command.Let_syntax in
     let open Core.Command.Param in
     let%map specdir = flag "-s" (required string) ~doc:"p4 spec directory"
     and includes_p4 = flag "-i" (listed string) ~doc:"p4 include paths"
     and excludes_p4 = flag "-e" (listed string) ~doc:"p4 test exclude paths"
     and testdir_p4 = flag "-d" (required string) ~doc:"p4 test directory"
     and negative = flag "-neg" no_arg ~doc:"use negative typing rules" in
     fun () ->
       run_il_test_driver negative specdir includes_p4 excludes_p4 testdir_p4)

(* Spec Structuring test *)

let structure specdir = specdir |> elab |> Structure.Struct.struct_spec

let structure_test specdir =
  let spec_sl = structure specdir in
  Sl.Print.string_of_spec spec_sl |> print_endline

let structure_command =
  Core.Command.basic ~summary:"run structuring test"
    (let open Core.Command.Let_syntax in
     let open Core.Command.Param in
     let%map specdir = flag "-s" (required string) ~doc:"p4 spec directory" in
     fun () ->
       try structure_test specdir with
       | ParseError (at, msg) -> Format.printf "%s\n" (string_of_error at msg)
       | ElabError (at, failtraces) ->
           Format.printf "%s\n" (string_of_elab_errors [ (at, failtraces) ]))

let command =
  Core.Command.group ~summary:"p4spec-test"
    [
      ("elab", elab_command);
      ("run-il", run_il_command);
      ("struct", structure_command);
      ("parser", run_parser_command);
    ]

let () = Command_unix.run ~version command
